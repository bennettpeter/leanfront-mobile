plugins {
    alias(libs.plugins.android.application)
}

task gitinfo {
    ext.describe = 'git describe --dirty'.execute().text.trim()
    ext.describe2 = 'git -C ../leanfront describe origin/master'.execute().text.trim()
    def split = ext.describe.split("-")
    def split2 = ext.describe2.split("-")
    ext.version = Integer.parseInt(split[1])
    ext.version2 = Integer.parseInt(split2[1])
    if (split.size() > 3)
        ext.dirty = '-' + split[3]
    else
        ext.dirty = ''
    ext.version = (ext.version + ext.version2) * 2 + 1
    ext.versionName = 'lfm-' + ext.version + '-' + split[2] + ext.dirty
    println "Version Name: " + ext.versionName + ", Version Code: " + ext.version
}

android {
    namespace 'org.mythtv.lfmobile'
    compileSdk 35
    lintOptions{
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "org.mythtv.leanfront"
        minSdk 23
        targetSdk 35
        versionCode gitinfo.ext.version
        versionName gitinfo.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable false
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        debug {
            minifyEnabled false
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }
    ndkVersion '28.2.13676358'

}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.recyclerview
    implementation libs.constraintlayout
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.navigation.fragment
    implementation libs.navigation.ui
    implementation libs.preference
    implementation libs.exifinterface
    implementation libs.lifecycle.process
//    implementation libs.firebase.crashlytics.buildtools
    implementation libs.legacy.support.v4
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation libs.preference.ktx
    implementation libs.swiperefreshlayout
    implementation libs.glide.v505

    // These are here as we build our own custom version
    implementation project(':media-lib-exoplayer')
    implementation project(':media-lib-ui')
    implementation project(':media-lib-common')
    implementation project(':media-lib-decoder-ffmpeg')

}